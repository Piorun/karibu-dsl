apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile("com.github.mvysny.dynatest:dynatest:${dynatest_version}")
    testCompile "com.github.kaributesting:karibu-testing-v8:$kaributesting_version"
    compile 'io.michaelrocks:bimap:1.0.2'

    // Vaadin
    compile "com.vaadin:vaadin-server:$vaadin8_version"
    compile "com.vaadin:vaadin-push:$vaadin8_version" // to include atmosphere and have support for class auto-discovery
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    // IDEA language injections
    compile 'com.intellij:annotations:12.0'

    // always include support for bean validation
    compile 'javax.validation:validation-api:2.0.0.Final'  // so that the BeanFieldGroup will perform JSR303 validations
    compile 'org.hibernate.validator:hibernate-validator:6.0.2.Final'
    // EL is required: http://hibernate.org/validator/documentation/getting-started/
    compile 'org.glassfish:javax.el:3.0.1-b08'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'karibu-dsl-v8'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Karibu-DSL, Kotlin extensions/DSL for Vaadin')
                root.appendNode('name', 'Karibu-DSL v8')
                root.appendNode('url', 'https://github.com/mvysny/karibu-dsl')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'github'
        name = 'com.github.vok.karibudsl'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/karibu-dsl'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
