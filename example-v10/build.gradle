plugins {
    id "com.github.ksoichiro.web.resource" version "1.7.3"
}

apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

def frontendWorkingDirectory = "src/main/webapp/frontend"

clean.doFirst {
    ['bower_components', 'build', 'node', 'node_modules', 'etc'].each {
        delete frontendWorkingDirectory + '/' + it
    }
}

repositories {
    maven { url "https://repo.vaadin.com/nexus/content/repositories/flow" }
}

processResources.dependsOn webResourceCompile

webResource {
    base {
        src "src/main/webapp"
        dest frontendWorkingDirectory
    }

    lib {
        dest "bower_components"
    }

    bower {
        dependencies {
            install name: "polymer", version: "2.1.1"
            install name: "paper-toast", version: "2.0.0"
            install name: "paper-dialog", version: "2.0.0"
            install name: "vaadin-core", version: "10.0.0-alpha6"
            copyAll true
        }
    }
}

gretty {
    contextPath = ''
}

dependencies {
    compile(project(":karibu-dsl-v10"))
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // logging
    // currently we are logging through the SLF4J API to LogBack. See src/main/resources/logback.xml file for the logger configuration
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'
    // this will allow us to configure Vaadin to log to SLF4J
    compile 'org.slf4j:jul-to-slf4j:1.7.25'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}
